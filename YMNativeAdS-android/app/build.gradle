apply plugin: 'com.android.application'

android {
	compileSdkVersion 25
	buildToolsVersion "25.0.1"
	defaultConfig {
		applicationId "net.youmi.ads.nativead.demo"
		minSdkVersion 14
		targetSdkVersion 25
		versionCode 2
		versionName "1.1.0"
		testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
		manifestPlaceholders = [APPLICATION_ID: applicationId]
		buildConfigField "String", "APPID", '\"000233cb7a375e27\"'
	}

	signingConfigs {
		release {
			keyAlias 'ymnativead'
			keyPassword 'ymnativead'
			storeFile file('keystore.jks')
			storePassword 'ymnativead'
		}
	}

	buildTypes {
		debug {
			signingConfig signingConfigs.release
		}
		release {
			minifyEnabled true
			zipAlignEnabled true
			shrinkResources true
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
			signingConfig signingConfigs.release
		}
	}
	applicationVariants.all { variant ->
		if (variant.buildType.name == 'release') {
			variant.outputs.each { output ->
				if (output.outputFile != null && output.outputFile.name.endsWith('.apk')) {
					if (output.outputFile.name.contains(variant.buildType.name)) {
						def date = new Date().format('yyyy-MM-dd')
						def srcDir = new File(project.rootProject.projectDir, "static/apk")
						def apkFile = new File(srcDir,
								"YmNativeAdsDemo_${variant.buildType.name}_v${variant.versionName}_${date}.apk")
						output.outputFile = apkFile
					}
				}
			}
		}
	}
}

dependencies {
	testCompile 'junit:junit:4.12'
	androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
		exclude group: 'com.android.support', module: 'support-annotations'
	})
	compile fileTree(include: ['*.jar'], dir: 'libs')
	compile 'com.android.support:appcompat-v7:25.3.1'
	compile 'com.android.support:cardview-v7:25.3.1'
	compile 'com.android.support:recyclerview-v7:25.3.1'
	compile 'com.android.support:design:25.3.1'

	// 使用Glide图片加载库来加载图片
	compile 'com.github.bumptech.glide:glide:3.7.0'

	// 使用permissiondispatcher来处理Android 6.0+运行时权限
	compile 'com.github.hotchemi:permissionsdispatcher:2.3.2'
	annotationProcessor 'com.github.hotchemi:permissionsdispatcher-processor:2.3.2'

	// 依赖本地project
	debugCompile project(path: ':library', configuration: 'debug')
	releaseCompile project(path: ':library', configuration: 'release')

	// 依赖本地aar
	// compile(name: 'nativead-release-1.0.0', ext: 'aar')

	// 依赖线上aar
	// compile 'net.youmi.ads:nativead:1.0.0@aar'


}

// 依赖本地aar时开启注释(测试用)
//repositories {
//	flatDir {
//		dirs '../static/aar' //this way we can find the .aar file in libs folder
//	}
//}